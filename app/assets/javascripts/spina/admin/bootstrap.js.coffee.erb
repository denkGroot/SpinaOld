# Bootstrapping all of Spina
ready = ->

  # Scroll
  setTimeout ->
    window.scrollTo(0)
  , 50

  # Select dropdowns
  $('select').select_skin()

  # Gallery select
  $('.gallery-select').galleryselect()

  # Datepicker
  $('input[data-datepicker]').datepicker
    dateFormat: 'dd MM yy',
    onClose: (dateText, inst) ->
      $(inst.input).change().focusout()

  # Fast buttons
  $('a.button:not(#nav-trigger)').addClass('fastbutton')

  # File input
  $('input[data-customfile]').each ->
    $(this).customFileInput()

  # Loading footable
  $('.footable').footable()

  # Loading switches
  $('input[data-switch]').spinaSwitch()

  # Load login form
  $('#login_wrapper').css('margin-top', ($(document).innerHeight() / 2) - ($('#login_wrapper').innerHeight() / 2) + 'px')

  # Wysihtml5 editor
  $('.wysihtml5-container').each ->
    textarea = $(this).find('textarea')
    toolbar = $(this).find('.toolbar')

    editor = new wysihtml5.Editor textarea.attr('id'), {
      toolbar: toolbar.attr('id'),
      useLineBreaks: false,
      parserRules: wysihtml5ParserRules,
    }

  # Loading epiceditor
  $('.epiceditor').each ->
    textarea = $(this)
    textarea.hide()
    id = 'editor_' + $(this).attr("id")
    textarea.parent().prepend("<div id=#{id} style='height: 250px' />")

    editor = new EpicEditor(
      container: id
      basePath: '/assets/spina/admin'
      clientSideStorage: false
      theme:
        base: "/epiceditor/base.css"
        preview: "/epiceditor/preview.css"
        editor: "/epiceditor/editor.css"

    ).load()
    editor.importFile('file', textarea.val())

    editor.on 'save', ->
      textarea.val(editor.exportFile())

  # Photo upload
  $('#new_photo').fileupload
    dataType: "script"
    limitMultiFileUploads: 10
    add: (e, data) ->
      types = /(\.|\/)(gif|jpe?g|png)$/i
      file = data.files[0]
      if types.test(file.type) || types.test(file.name)
        data.context = $(tmpl("template-upload", file))
        $('#uploads').append(data.context)
        data.submit()
      else
        modal = '
          <div id="modal" class="modal">
            <header class="big-header">
              <a href="#" class="close_modal" data-dismiss="modal"><i class="icon-only" data-icon="x"></i></a>
              <h3><strong>' + file.name + '</strong> is geen afbeelding.</h3>
            </header>
            <footer>
                <a href="#" data-dismiss="modal" data-icon="v" style="width: 100%; -webkit-box-shadow: none; border-left: none">Ok√©, begrepen</a>
              </footer>
          </div>
        '
        $(modal).modal()

    progress: (e, data) ->
      if data.context
        progress = -(parseInt(data.loaded / data.total * 100, 10) - 100)
        progress = 20 if progress < 20
        data.context.find('.upload-cloud-processing').css('height', progress + '%')

  $('#new_photo input').hover ->
    $("#uploadbutton").addClass('hover')
  , ->
    $("#uploadbutton").removeClass('hover')

  # Colorpickers
  $('.colorpicker').each ->
    colorpicker = $(this).find('.colorpicker-container')
    field = $(this).find('input')

    $.farbtastic colorpicker, (color) ->
      field.val(color)
      field.css('color', color)

  $('.colorpicker input').focus ->
    container = $(this).parent()
    colorpicker = container.find('.colorpicker-container')
    $.farbtastic(colorpicker).setColor($(this).val())
    container.find('.farbtastic').show()

  $('.colorpicker input').blur ->
    container = $(this).parent()
    container.find('.farbtastic').hide()

  $('form').on 'click', '.remove_field', (e) ->
    e.preventDefault()
    fieldset = $(this).closest('fieldset')
    fieldset.find('input[type=hidden]').val('1')
    fieldset.hide()

  $('form').on 'click', '.clear_field', (e) ->
    e.preventDefault()
    $(this).hide()
    fieldset = $(this).closest('fieldset')
    fieldset.find('input[type=file]').wrap('<form>').closest('form').get(0).reset()
    fieldset.find('input[type=file]').unwrap()
    fieldset.find('.customfile-feedback').text('Nog geen bestand geselecteerd')
    fieldset.find('input[type=hidden]').val(1)

  $('form').on 'click', '.add_fields', (e) ->
    time = new Date().getTime()
    regexp = new RegExp($(this).data('id'), 'g')
    $(this).before($(this).data('fields').replace(regexp, time))
    $(this).parent().find('input[type=file]').last().customFileInput()
    e.preventDefault()

$(document).ready(ready)
$(document).on('page:load', ready)